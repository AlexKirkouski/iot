MODULE DevEvent;

REQUIRE Organization;

CLASS CauseEvent 'Причина события' {
    network      'Сеть',
    powerSupply  'Источник питания',
    error        'Ошибка устройства',
    client       'Виноват клиент'
}


CLASS DevEvent 'События для устройств';
TABLE devEvent (DevEvent);

organization      'Организация'       = DATA Organization (DevEvent) ;
id                'ID'                = DATA LONG         (DevEvent) CHARWIDTH 8;
dt                'Дата события'      = DATA DATETIME     (DevEvent);
subject           'Событие'           = DATA BPISTRING    (DevEvent); 
body              'Текст сообщения'   = DATA BPISTRING    (DevEvent);
action            'Действие'          = DATA BPISTRING    (DevEvent);
deviceReplacement 'ID замены'         = DATA LONG         (DevEvent) ;
description       'Описание'          = DATA BPISTRING    (DevEvent);

causeEvent = DATA CauseEvent (DevEvent);
nameCauseEvent    'Причина' (DevEvent e) = staticCaption(causeEvent(e)); 

FORM devEvents 'Лог событий'
    OBJECTS o = Organization
    PROPERTIES(o) READONLY name, unn, address

    OBJECTS d = DevEvent
    PROPERTIES(d) READONLY dt, id, subject, body, action
    PROPERTIES(d) nameCauseEvent, description, deviceReplacement 
    FILTERS organization(d) = o
    ORDER dt(d) DESC 
;

DESIGN devEvents {
    NEW split {
        type = SPLITV;
        NEW types {
            type = SPLITH ;
            MOVE BOX(o) {
                caption = 'Организации';
                flex = 1;
            }
            fill = 1;
        }
        NEW tab {
            type = SPLITH ;
            MOVE BOX(d) {
                caption = 'Лог событий';
                flex = 1;
            }
            fill = 2;
        }
        fill = 1;
    }
    MOVE TOOLBARBOX;
}
