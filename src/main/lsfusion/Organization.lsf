MODULE Organization;

REQUIRE Options, SystemEvents, Utils;

CLASS Organization 'Организация';
TABLE organization(Organization);
// основные
name            'Название организации'              = DATA STRING[60]   (Organization) NONULL;
unn             'УНН'                               = DATA STRING[10]   (Organization) NONULL;
address         'Адрес'                             = DATA STRING[100]  (Organization) NONULL;
contact         'Контактное лицо'                   = DATA STRING[40]   (Organization) NONULL;
phone           'Телефон для связи'                 = DATA STRING[40]   (Organization) NONULL;
site            'Сайт организации'                  = DATA STRING[100]  (Organization) NONULL;
email           'Электронный адрес'                 = DATA STRING[100]  (Organization) NONULL;
partner         'Партнер'                           = DATA BOOLEAN      (Organization);
// логирование
dateCreation    'Дата создания'                     = DATA DATETIME     (Organization);
userCreation    'Кто создал'                        = DATA BPSTRING[100](Organization);
// свойства только для grafana
orgId           'ID организации'                    = DATA INTEGER      (Organization); // RO
dsId            'ID datasource'                     = DATA INTEGER      (Organization); // RO
dsUrl           'URL адрес'                         = DATA STRING[100]  (Organization) ;
dsName          'Имя DataSource'                    = DATA STRING[20]   (Organization); // RO 
dsNameDB        'Имя БД'                            = DATA STRING[50]   (Organization) ;
dsLogin         'Логин к БД'                        = DATA STRING[20]   (Organization) ;
dsPassword      'Пароль'                            = DATA STRING[20]   (Organization) ;
dsSSL           'SSL режим'                         = DATA STRING[15]   (Organization) ;
adminId         'ID админа'                         = DATA INTEGER      (Organization); // RO
admin           'Логин администратора'              = DATA STRING[20]   (Organization) ;
adminPassword   'Пароль администратора'             = DATA STRING[20]   (Organization) ;
userId          'ID пользователя'                   = DATA INTEGER      (Organization); // RO  
user            'Логин пользователя'                = DATA STRING[20]   (Organization) ;
userPassword    'Пароль пользователя'               = DATA STRING[20]   (Organization) ;
grName          'Название организации в Grafana'    = DATA STRING[60]   (Organization) ;

// запрет наличия пробелов в названии организации для Графаны
CONSTRAINT CHANGED(grName(Organization o)) AND strpos(grName(o), ' ') > 0
    MESSAGE 'В названии организации для Grafana должны отсутствовать пробелы !';
// запрет наличия двойных кавычек в названии организации для Графаны
CONSTRAINT CHANGED(grName(Organization o)) AND strpos(grName(o), '"') > 0
    MESSAGE 'В названии организации для Grafana должны отсутствовать кавычки !';

// уникальное значение имени при создании (изменении)
organization = GROUP AGGR Organization o BY name(o);

WHEN SET (Organization o IS Organization) DO {
    dsUrl(o) <- grafanaPsUrl();
    dsNameDB(o) <- grafanaPsDB();
    dsLogin(o) <- grafanaPsLogin();
    dsPassword(o) <- grafanaPsPwd();
    dsSSL(o) <- grafanaPsSSL();
    dateCreation(o) <- currentDateTime();
    userCreation(o) <- login(currentUser());
}

// Организация - для каждого юзера
organization 'Организация' = DATA Organization (CustomUser);
nameOrganization 'Организация' (CustomUser u) = name(organization(u)) CHARWIDTH 30;

CLASS SubUnit 'Подразделение';
TABLE subunit(SubUnit);
// основные
orgId           'Организация'               = DATA Organization (SubUnit) AUTOSET; // поле связи, ключевое слово AUTOSET
name            'Название подразделения'    = DATA STRING[60]   (SubUnit) NONULL;
country         'Страна'                    = DATA STRING[30]   (SubUnit) NONULL;
typePoint       'Тип н/пункта'              = DATA STRING[30]   (SubUnit) NONULL;
pointName       'Название н/пункта'         = DATA STRING[50]   (SubUnit) NONULL;
typeStreet      'Тип улицы'                 = DATA STRING[20]   (SubUnit) NONULL;
streetName      'Название улицы'            = DATA STRING[50]   (SubUnit) NONULL;  
house           'Дом'                       = DATA STRING[10]   (SubUnit) NONULL;
contact         'Контактное лицо'           = DATA STRING[40]   (SubUnit) NONULL;
phone           'Телефон для связи'         = DATA STRING[40]   (SubUnit) NONULL;
// логирование
dateCreation    'Дата создания'             = DATA DATETIME     (SubUnit);
userCreation    'Кто создал'                = DATA BPSTRING[100](SubUnit);

WHEN SET (SubUnit s IS SubUnit) DO {
    dateCreation(s) <- currentDateTime();
    userCreation(s) <- login(currentUser());
}

CLASS Contract 'Договор';
TABLE contract(Contract);
// Основные
orgId           'Организация'               = DATA Organization     (Contract) AUTOSET; // поле связи, ключевое слово AUTOSET
number          'Номер'                     = DATA STRING[20]       (Contract) NONULL;  
date            'Дата'                      = DATA DATE             (Contract) NONULL;
comment         'Примечание'                = DATA STRING[100]      (Contract); 
// логирование
dateCreation    'Дата создания'             = DATA DATETIME         (Contract);
userCreation    'Кто создал'                = DATA BPSTRING[100]    (Contract);

WHEN SET (Contract c IS Contract) DO {
    dateCreation(c) <- currentDateTime();
    userCreation(c) <- login(currentUser());
}


FORM organization 'Организации'
    OBJECTS o = Organization
    PROPERTIES(o) READONLY name, unn, address
    PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE
    OBJECTS s = SubUnit
    PROPERTIES(s) READONLY name, contact, phone, country, streetName, house
    PROPERTIES(s) NEWSESSION NEW, EDIT, DELETE 
    FILTERS orgId(s) = o 
    OBJECTS c = Contract
    PROPERTIES(c) READONLY number, date, comment 
    PROPERTIES(c) NEWSESSION NEW, EDIT, DELETE 
    FILTERS orgId(c) = o  
;

DESIGN organization {
    OBJECTS {
        NEW split {
            type = SPLITV;
            NEW org {
                type = SPLITH ;
                MOVE BOX(o) {
                    caption = 'Организации';
                    flex = 1;
                }
                fill = 2;
            }
            NEW tab {
                type = SPLITH ;
                MOVE BOX(s) {
                    caption = 'Подразделения';
                    flex = 8;
                }
                MOVE BOX(c) {
                    caption = 'Договора';
                    flex = 7;
                }
                fill = 2;
            }
            fill = 1;
        }
    }
}

FORM editOrganization 'Редактирование организации'
    OBJECTS o = Organization PANEL
    PROPERTIES(o) name,unn,address,contact,phone,site,email,partner
    PROPERTIES(o) READONLY userCreation, dateCreation
    EDIT Organization OBJECT o
;

FORM organizations 'Организации'
    OBJECTS o=Organization
    PROPERTIES(o) READONLY name, unn, address
    LIST Organization OBJECT o
;

DESIGN editOrganization {
    NEW cnt FIRST {
        fill = 1;
//        type = COLUMNS;
        NEW h1 {
            type = CONTAINERV;
            caption = 'Общие сведения';
            MOVE PROPERTY (name(o));
            MOVE PROPERTY (unn(o));
            MOVE PROPERTY (address(o));
            MOVE PROPERTY (contact(o));
            MOVE PROPERTY (phone(o));
            MOVE PROPERTY (site(o));
            MOVE PROPERTY (email(o));
            MOVE PROPERTY (partner(o));
        } 
        NEW h2 AFTER h1 {
            type = CONTAINERV;
            caption = 'Создано';
            MOVE PROPERTY(userCreation(o));
            MOVE PROPERTY(dateCreation(o));
        }
        MOVE TOOLBAR ; 
    }
}

FORM editSubUnit 'Редактирование подразделения'
    OBJECTS s = SubUnit PANEL
    PROPERTIES(s) name, country, typePoint, pointName, typeStreet, streetName, house, contact, phone
    PROPERTIES(s) READONLY userCreation, dateCreation
    EDIT SubUnit OBJECT s
;

FORM SubUnits 'Подразделения'
    OBJECTS s = SubUnit
    PROPERTIES(s) READONLY name, contact, phone, country, typePoint, pointName, typeStreet, streetName, house
    LIST SubUnit OBJECT s
;

DESIGN editSubUnit {
    NEW cnt FIRST {
        fill = 1;
        NEW h1 {
            type = CONTAINERV;
            caption = 'Общие сведения';
            MOVE PROPERTY (name(s));
            MOVE PROPERTY (contact(s));
            MOVE PROPERTY (phone(s));
            MOVE PROPERTY (country(s));
            MOVE PROPERTY (typePoint(s));
            MOVE PROPERTY (pointName(s));
            MOVE PROPERTY (typeStreet(s));
            MOVE PROPERTY (streetName(s));
            MOVE PROPERTY (house(s));
        }
        NEW h2 AFTER h1 {
            type = CONTAINERV;
            caption = 'Создано';
            MOVE PROPERTY(userCreation(s));
            MOVE PROPERTY(dateCreation(s));
        }
        MOVE TOOLBAR ; 
    }    
}

FORM editContract 'Редактирование договоров'
    OBJECTS c = Contract PANEL
    PROPERTIES(c) number, date, comment
    PROPERTIES(c) READONLY dateCreation, userCreation
    EDIT Contract OBJECT c
;

DESIGN editContract {
    NEW cnt FIRST {
        fill = 1;
        NEW h1 {
            type = CONTAINERV;
            caption = 'Общие сведения';
            MOVE PROPERTY (number(c));
            MOVE PROPERTY (date(c));
            MOVE PROPERTY (comment(c));
        }
        NEW h2 AFTER h1 {
            type = CONTAINERV;
            caption = 'Создано';
            MOVE PROPERTY(userCreation(c));
            MOVE PROPERTY(dateCreation(c));
        }
        MOVE TOOLBAR ; 
    }    
}
