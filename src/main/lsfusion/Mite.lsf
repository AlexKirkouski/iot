MODULE Mite;

REQUIRE Options, Organization, 
        OrgDevice, OrgMeasurement, 
        OrgController, OrgControllerDevice,
        OrgManagement,
        ProcessMonitor, SQLUtils, ProcessUtils, SchedulerDefaultData, Backup, Eval,
        GrafanaDevice,
        MqttServer, Server,
        Export, 
        Telegram, Viber, Skype, Slack;

simID 'Sim ID' = DATA STRING (Device);
EXTEND FORM deviceTypes
    PROPERTIES (d) simID
;
writeSimID(LONG l, STRING simID) + {
    APPLY FOR Device d = device(l) DO
        simID(d) <- simID;
}
deviceBySimID = GROUP AGGR Device d BY simID(d);

simID 'Sim ID' = DATA STRING (Controller);
EXTEND FORM controllerTypes
    PROPERTIES (c) simID
;

temperatureType = GROUP AGGR MeasurementType type WHERE name(type) = 'Температура';
humidityType = GROUP AGGR MeasurementType type WHERE name(type) = 'Влажность';
batteryType = GROUP AGGR MeasurementType type WHERE name(type) = 'Батарея';

maxFixedValue 'Макс. фикс. значение' (ManagementDevice md, MeasurementType mt) = GROUP MAX OVERRIDE value(right(ValuesCondition cv)) IF device(left(cv)) = md AND measurementType(left(cv)) = mt,
                                                                   value(left(cv)) IF device(right(cv)) = md AND measurementType(right(cv)) = mt;
minFixedValue 'Мин. фикс. значение' (ManagementDevice md, MeasurementType mt) = GROUP MIN OVERRIDE value(right(ValuesCondition cv)) IF device(left(cv)) = md AND measurementType(left(cv)) = mt,
                                                                   value(left(cv)) IF device(right(cv)) = md AND measurementType(right(cv)) = mt;
                                                                   
GROUP settings 'Настройки' : public;                                                   
measurementPeriod 'Период измерений' = DATA NUMERIC (Device) IN settings;
transmissionPeriod 'Период передачи' = DATA NUMERIC (Device) IN settings;
maxTemperature 'Макс. температура' (Device d) = maxFixedValue(d AS Device, temperatureType()) IN settings;
maxHumidity 'Макс. влажность' (Device d) = maxFixedValue(d AS Device, humidityType()) IN settings;
minTemperature 'Мин. температура' (Device d) = minFixedValue(d AS Device, temperatureType()) IN settings;
minHumidity 'Мин. влажность' (Device d) = minFixedValue(d AS Device, humidityType()) IN settings;
adjustmentTemperature 'Температура корр.' (Device d) = importAdjustmentValue(d AS Device, temperatureType()) IN settings;
adjustmentHumidity 'Влажность корр.' (Device d) = importAdjustmentValue(d AS Device, humidityType()) IN settings;

updateDeviceSettings 'Обновить' INTERNAL 'mite.UpdateDeviceSettingsAction' (Device) IN settings; 
WHEN CHANGED (measurementPeriod(Device d)) OR CHANGED (transmissionPeriod(d)) OR 
        CHANGED (maxTemperature(d)) OR CHANGED (maxHumidity(d)) OR
        CHANGED (adjustmentTemperature(d)) OR CHANGED (adjustmentHumidity(d)) OR
        CHANGED (minTemperature(d)) OR CHANGED (minHumidity(d)) DO
    updateDeviceSettings(d);
    
EXTEND FORM devices
    PROPERTIES (d) PANEL measurementPeriod, transmissionPeriod, minTemperature, maxTemperature, minHumidity, maxHumidity, adjustmentTemperature, adjustmentHumidity, updateDeviceSettings
;

DESIGN devices {
    tab {
        NEW settings {
            caption = 'Настройки'; 
            MOVE GROUP (settings, d) { type = CONTAINERV; };
        }
    }
}

GROUP root;
GROUP result : root;

GROUP json;

//code = DATA LOCAL STRING(INTEGER);
date = DATA LOCAL DATETIME (INTEGER);
imei = DATA LOCAL STRING(INTEGER);
value = DATA LOCAL STRING(INTEGER);

countImei = GROUP SUM 1 BY imei(INTEGER row);

FORM importResults
    OBJECTS items = INTEGER IN result

//    PROPERTIES(items) code
    PROPERTIES(items) IN json date, value, imei
    FILTERS imported(items);
    
GROUP nav 'Параметры импорта БТС';
navLogin 'Логин доступа' = DATA STRING () IN nav;
navPassword 'Пароль доступа' = DATA STRING () IN nav;
lastImportTime 'Последнее время импорта' = DATA DATETIME () IN nav;

importMeasurements 'Импортировать измерения' () {
    LOCAL token = STRING (); 

    EXTERNAL HTTP 'https://api.nav.by/info/integration_get_token.php?type=GET_TOKEN&login=$1&password=$2' PARAMS navLogin(), navPassword() TO exportFile;
    IF failedHttp() THEN {
        MESSAGE 'Unable to get token ' + statusHttp() + '\n' + STRING(JSONFILE(exportFile())) NOWAIT;
        RETURN;
    }
    
    IMPORT JSON FROM exportFile() TO () token;
    
    EXTERNAL HTTP 'https://api.nav.by/info/integration.php?type=EVENTS&token=$1&event_codes=temp_sensor1&datetime=$2&limit=ALL' PARAMS token(), (OVERRIDE lastImportTime(),1982_01_01_12:00) TO exportFile;

    IF failedHttp() THEN {
        MESSAGE 'Unable to get data ' + statusHttp() + '\n' + STRING(JSONFILE(exportFile())) NOWAIT;
        RETURN;
    }
    
    IMPORT importResults JSON FROM exportFile();
    
    LOCAL missingImeis = STRING ();
    missingImeis() <- GROUP CONCAT STRING imei IF countImei(imei) AND NOT deviceBySimID(imei), ', ' ORDER imei;
    IF missingImeis() THEN {
        MESSAGE 'IMEIs not found : ' + missingImeis() NOWAIT;        
        RETURN;
    }
    
    FOR Device dv = deviceBySimID(imei(INTEGER row)) DO {
        NEW m = Measurement { 
            dateTime(m) <- ZDATETIME(date(row)); 
            device(m) <- dv; 
            idImport(m) <- LONG(imei(row)); 
            unAdjustedValue(m, temperatureType()) <- DOUBLE(value(row)); 
        }
    }
    lastImportTime() <- currentDateTime();
    APPLY;
}

GROUP ftp 'Параметры импорта FTP';
importFTPPath 'Путь к файлу импорта' = DATA STRING () IN ftp;

importFTPMeasurements 'Импортировать измерения' () {
    LOCAL token = STRING (); 

    READ importFTPPath() TO exportFile;
//    IF failedHttp() THEN {
//        MESSAGE 'Unable to get token ' + statusHttp() + '\n' + STRING(JSONFILE(exportFile())) NOWAIT;
//        RETURN;
//    }
    
    LOCAL eui64 = STRING (INTEGER);
    LOCAL tmmeasure = LONG (INTEGER);
    LOCAL temperatur = DOUBLE (INTEGER);
    LOCAL humidity = DOUBLE (INTEGER);
    LOCAL battery = DOUBLE (INTEGER);
    IMPORT DBF FROM exportFile() TO eui64, tmmeasure, temperatur, humidity, battery 
    
    LOCAL missingEui64 = STRING ();
    missingEui64() <- GROUP CONCAT STRING eui64 IF [GROUP SUM 1 IF eui64(INTEGER row) = eui64]() AND NOT deviceBySimID(eui64), ', ' ORDER eui64;
    IF missingEui64() THEN {
        MESSAGE 'EUI64s not found : ' + missingEui64() NOWAIT;        
        RETURN;
    }
    
    FOR Device dv = deviceBySimID(eui64(INTEGER row)) DO {
        NEW m = Measurement { 
            dateTime(m) <- toZDateTime(tmmeasure(row)); 
            device(m) <- dv; 
//            idImport(m) <- LONG(eui64(row)); 
            unAdjustedValue(m, temperatureType()) <- DOUBLE(temperatur(row)); 
            unAdjustedValue(m, humidityType()) <- DOUBLE(humidity(row)); 
            unAdjustedValue(m, batteryType()) <- DOUBLE(battery(row)); 
        }
    }
    APPLY;
    IF NOT canceled() THEN 
        delete(importFTPPath());
}

EXTEND FORM options
    PROPERTIES () navLogin, navPassword, lastImportTime, importMeasurements, importFTPPath, importFTPMeasurements
;
DESIGN options {
    pane {
        NEW hnav {
            caption = 'Параметры импорта БТС'; 
            MOVE PROPERTY(navLogin());
            MOVE PROPERTY(navPassword());
            MOVE PROPERTY(lastImportTime());
            MOVE PROPERTY(importMeasurements());
        }
        NEW hftp {
            caption = 'Параметры импорта FTP'; 
            MOVE PROPERTY(importFTPPath());
            MOVE PROPERTY(importFTPMeasurements());
        }
    }
}

FORM printMeasurements
    OBJECTS o = Organization PANEL
    PROPERTIES name(o)
    OBJECTS m = Measurement
    FILTERS organization(m) = o
    PROPERTIES deviceId(m), dateTime(m), nameDeviceType(m), temp = value(m, temperatureType()), hum = value(m, humidityType())
;

printMeasurements 'Распечатать измерения (NEW)' (Organization o) {
    DIALOG dialogDates OBJECTS dFrom INPUT, dTo INPUT DO
        PRINT printMeasurements OBJECTS o = o FILTERS dateTime(m) >= ZDATETIME(dFrom) AND dateTime(m) <= ZDATETIME(dTo);
}

EXTEND FORM organizations
    PROPERTIES (o) printMeasurements TOOLBAR
;