MODULE OrgDevice;

REQUIRE Organization, Device, Options, Utils;

organization 'Организация' = DATA Organization (Device) ;
subUnit 'Точка размещения' = DATA SubUnit (Device) ;
nameSubUnit 'Точка размещения' = name(subUnit(Device d));
contract 'Договор' = DATA Contract (Device) ;
numberContract 'Договор' = number(contract(Device d));
 
placeInstallation 'Место установки'  = DATA BPSTRING[200] (Device);
note 'Примечание'  = DATA BPSTRING[200] (Device);

country         'Страна'                    = country(subUnit(Device d)) ;
typePoint       'Тип н/пункта'              = typePoint(subUnit(Device d));
pointName       'Название н/пункта'         = pointName(subUnit(Device d));
typeStreet      'Тип улицы'                 = typeStreet(subUnit(Device d));
streetName      'Название улицы'            = streetName(subUnit(Device d));  
house           'Дом'                       = house(subUnit(Device d));

FORM orgDevices 'Организации и их датчики'
    OBJECTS o = Organization
    PROPERTIES(o) READONLY name, unn, address
    
    OBJECTS d = Device
    PROPERTIES(d) select
    PROPERTIES(d) READONLY id, dateCreation, nametype, dbId
    PROPERTIES(d) nameSubUnit, numberContract, placeInstallation, note 
    ORDER nametype(d), id(d)
    FILTERS organization(d) = o
;

DESIGN orgDevices {
    NEW split {
        type = SPLITV;
        NEW types {
            type = SPLITH ;
            MOVE BOX(o) {
                caption = 'Организации';
                flex = 1;
            }
            fill = 1;
        }
        NEW tab {
            type = SPLITH ;
            MOVE BOX(d) {
                caption = 'Устройства';
                flex = 1;
            }
            fill = 2;
        }
        fill = 1;
    }
    MOVE TOOLBARBOX;
}

///// Редактирование полей организации для Grafana
resetValues 'Сброс значений созданных параметров' (Organization o){
    ASK 'Перед сбросом значений не забудьте удалить организацию в Grafana ! \n\n' + 
        'Вы действительно хотите сбросить значения созданных параметров ?' yes = YESNO DO {
        IF yes THEN {
            orgId(o)   <- NULL ;
            dsId(o)    <- NULL ;
            dsName(o)  <- NULL ;
            adminId(o) <- NULL ;
            userId(o)  <- NULL ;
        }
    }
}

FORM editOrgGrafana 'Редактирование организации'
    OBJECTS o = Organization PANEL
    PROPERTIES(o) name READONLY 
    PROPERTIES(o) READONLY orgId,dsId,dsName,adminId,userId
    PROPERTIES(o) grName,dsUrl,dsNameDB,dsLogin,dsPassword,dsSSL,admin,adminPassword,user,userPassword
    PROPERTIES(o) resetValues
;

editOrgGrafana 'Редактировать' (Organization o) {
    NEWSESSION {
        SHOW editOrgGrafana ;
    }
}
EXTEND FORM orgDevices
    PROPERTIES(o) editOrgGrafana;

DESIGN editOrgGrafana {
    NEW cnt FIRST {
        fill = 1;
        MOVE PROPERTY (name(o));
        MOVE PROPERTY (grName(o));
        NEW cnt2 {
            fill = 1;
            type = COLUMNS;
            NEW h1 {
                type = CONTAINERV;
                caption = 'Пароли в Grafana'; 
                MOVE PROPERTY (admin(o));
                MOVE PROPERTY (adminPassword(o));
                MOVE PROPERTY (user(o));
                MOVE PROPERTY (userPassword(o));
            }
            NEW h2 AFTER h1{
                type = CONTAINERV;
                caption = 'Связь с PostgreSQL'; 
                MOVE PROPERTY (dsUrl(o));
                MOVE PROPERTY (dsNameDB(o));
                MOVE PROPERTY (dsLogin(o));
                MOVE PROPERTY (dsPassword(o));
                MOVE PROPERTY (dsSSL(o));
            }
            NEW h3 AFTER h2 {
                type = CONTAINERV;
                caption = 'Контроль созданных параметров';
                MOVE PROPERTY (orgId(o));
                MOVE PROPERTY (dsName(o));
                MOVE PROPERTY (dsId(o));
                MOVE PROPERTY (adminId(o));
                MOVE PROPERTY (userId(o));
                MOVE PROPERTY (resetValues(o));
            }
            MOVE TOOLBAR ;
        } 
    }
}

/////

///// Ввод новых устройств по организации (устройств, которые не принадлежат организации)
FORM insertDevices 'Свободные устройства'
    OBJECTS d = Device
    PROPERTIES(d) select
    PROPERTIES(d) READONLY id, nametype, dateCreation
    ORDER nametype(d), id(d)
    FILTERS NOT organization(d)
;

insertDevicesOrg 'Добавить устройства' (Organization o) {
    NEWSESSION {
        SHOW insertDevices ;
        FOR select(Device d) AND NOT organization(d) DO {
            organization(d) <- o;
            select(d) <- NULL;
        }
        APPLY ;
    }
}

EXTEND FORM orgDevices
    PROPERTIES(o) insertDevicesOrg ;
/////


///// Удаление отмеченных по организации устройств
deleteDevicesOrg 'Удалить отмеченные устройства' (Organization o) {
    NEWSESSION NESTED(select) {
        FOR select(Device d) AND organization(d) = o DO {
            organization(d) <- NULL;
        }
        APPLY ;
    }
}

EXTEND FORM orgDevices
    PROPERTIES(o) deleteDevicesOrg ;
/////