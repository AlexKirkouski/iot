MODULE Server;

REQUIRE Device, SystemEvents, Measurement;

CLASS Server 'Сервер';
name 'Наименование сервера' =  DATA STRING[100] (Server) CHARWIDTH 70;
port 'Порт' = DATA INTEGER (Server);
isRunning 'Запущен' = DATA BOOLEAN (Server);

startUDPServer INTERNAL 'mite.StartUDPServerAction' (Server, INTEGER, TEXT);
stopUDPServer INTERNAL 'mite.StopUDPServerAction' (Server);

startServer 'Запустить сервер' (Server server) {    
    APPLY isRunning(server) <- TRUE;
    
    startUDPServer(server, port(server), 'importCSV');
}
stopServer 'Остановить сервер' (Server server) {
    stopUDPServer(server);

    APPLY isRunning(server) <- NULL;
}

server = DATA Server (Measurement);

FORM servers 'Сервера' 
    OBJECTS s = Server
    PROPERTIES (s) name, port, NEW, DELETE
    PROPERTIES (s) TOOLBAR startServer SHOWIF NOT isRunning(s), stopServer SHOWIF isRunning(s)
    
    OBJECTS (m = Measurement, mt = MeasurementType)
    PROPERTIES (m) dateTime 
    PROPERTIES 'ИД'=(OVERRIDE deviceId(m)+'', idImport(m) + '(не найдено)')
    PROPERTIES value(m, mt) 
    ORDER dateTime(m) DESC
    FILTERS server(m) = s  
;

NAVIGATOR {
    NEW servers;
}

onStarted() + {
    FOR isRunning(Server s) DO {
        startUDPServer(s, port(s), 'importCSV');
    }
}

// IMPORT
fieldImport(n, DeviceType dt, MeasurementType mt) = 'field' + (n IF n=number(dt, mt));
importCSVScript 'Скрипт импорта' (DeviceType dt) = 'run(Server server, CSVFILE file) \{\n'+
                                                '\t'+  'IMPORT CSV NOHEADER FROM file FIELDS DATETIME dt, LONG id' + (OVERRIDE (GROUP CONCAT ', DOUBLE '+fieldImport(n, dt, MeasurementType mt), '' ORDER n),'') + ' DO \{\n' +
                                                '\t\t'+       'NEW m = Measurement \{\n' +
                                                '\t\t\t'+            'dateTime(m) <- dt;\n' +
                                                '\t\t\t'+            'device(m) <- device(id);\n' +
                                                '\t\t\t'+            'idImport(m) <- id;\n' +
                                                '\t\t\t'+            'server(m) <- server;\n' +
                                      (OVERRIDE('\t\t\t'+            'value(m, MeasurementType mt) <- CASE EXCLUSIVE' + (GROUP CONCAT ' WHEN LONG(mt)=' + LONG(MeasurementType mt) + ' THEN ' + fieldImport(n, dt, mt),'' ORDER n) + ';\n'),'') + 
                                                '\t\t'+       '\}\n' + 
                                                '\t'+  '\}\n' +
                                                '\t'+  'APPLY;\n' +
                                                '\}\n';

EXTEND FORM deviceTypes
    PROPERTIES (t) importCSVScript
;                                                 

importCSV(DeviceType dt, Server server, CSVFILE file) {
    EVAL importCSVScript(dt) PARAMS server,  file;
}

// TEST
importCSVTest(DeviceType dt) {
    INPUT f = CSVFILE DO {
        EXEC importCSV(dt, NULL, f) ;
    }
}
EXTEND FORM deviceTypes
    PROPERTIES importCSVTest(t) TOOLBAR;
