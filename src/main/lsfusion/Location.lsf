MODULE Location;

REQUIRE Controller, Device, Geo;

CLASS Location 'Точка размещения' : POI;
name 'Название' = DATA STRING[60] (Location) NONULL IN id;
address 'Адрес' = DATA STRING[50] (Location) IN id;
latitude 'Широта' = DATA NUMERIC[11,6] (Location) IN id;
longitude 'Долгота' = DATA NUMERIC[11,6] (Location) IN id;

mainAddress(Location l) += address(l);
latitude(Location l) += latitude(l);
longitude(Location l) += longitude(l);

FORM locations 'Точки размещения'
    OBJECTS l = Location
    PROPERTIES(l) name, address, latitude, longitude, calculateCoordinates GRID, NEW, DELETE;
;

DESIGN locations {
    OBJECTS {
        NEW top {
            type = CONTAINERV;
            MOVE BOX(l);
            fill = 1;
        }
    }
}

NAVIGATOR {
    equipment {
        NEW FOLDER location 'Расположение' {
            NEW locations;
        }
    }
}

location = DATA Location (Controller);
nameLocation 'Точка размещения' = name(location(Controller c));
EXTEND FORM controllers
    PROPERTIES (c) nameLocation
;

location = DATA Location (Device);
nameLocation 'Точка размещения' = name(location(Device d));
latitude 'Широта' (Device d) = latitude(location(d));
longitude 'Долгота' (Device d) = longitude(location(d));
EXTEND FORM devices
    PROPERTIES (d) nameLocation, latitude = latitude, longitude = longitude
;
