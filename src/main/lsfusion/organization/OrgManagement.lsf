MODULE OrgManagement;

REQUIRE OrgDevice, Management, OrgControllerDevice, OrgLocation;

NAMESPACE ControllerManagement;

organization = DATA Organization (ControllerManager) AUTOSET;

@addOrganization(controllerManagers, cm);

organization 'Организация' = ABSTRACT Organization (ConditionValue);

organization(MeasurementConditionValue v) += organization(device(v));
organization = DATA Organization (FixedConditionValue) AUTOSET;
organization(FixedConditionValue v) += organization(v);
organization(DelayConditionValue v) += organization(device(v));

organization = DATA Organization (AverageMeasurementConditionValue) AUTOSET;
organization(AverageMeasurementConditionValue v) += organization(v);
organization = DATA Organization (InAreaConditionValue) AUTOSET;
organization(InAreaConditionValue v) += organization(v);
organization(LastChangeConditionValue v) += organization(manager(v));
organization(StateConditionValue v) += organization(manager(v));

changeDevice(AverageMeasurementConditionValue av) +{
    DIALOG devices OBJECTS o=organization(av), d=device(av) CHANGE;
}
changeDevice(InAreaConditionValue av) +{
    DIALOG devices OBJECTS o=organization(av), d=device(av) CHANGE;
}
CONSTRAINT organization(area(InAreaConditionValue av)) != organization(av) CHECKED BY area[InAreaConditionValue] MESSAGE 'Область условия не принадлежит организации условия';

EXTEND FORM averageConditionValue
    OBJECTS o = Organization BEFORE v
;
EXTEND FORM inAreaConditionValue
    OBJECTS o = Organization BEFORE v
;

change(AverageMeasurementConditionValue av) +{
    DIALOG averageConditionValue OBJECTS o=organization(av), v=av;
} 
change(InAreaConditionValue av) +{
    DIALOG inAreaConditionValue OBJECTS o=organization(av), v=av;
} 

EXTEND FORM conditionValues
    OBJECTS o = Organization BEFORE v
    FILTERS organization(v) = o
;

EXTEND FORM controllerManagers
    FILTERS organization(cv) = o
;

dialogChangeLeftConditionValue(ValuesCondition vc) + {
    DIALOG conditionValues OBJECTS o = organization(controller(vc)), v = left(vc) CHANGE;
}
dialogChangeRightConditionValue(ValuesCondition vc) + {
    DIALOG conditionValues OBJECTS o = organization(controller(vc)), v = right(vc) CHANGE;
} 

organization = DATA Organization (ManagerTo) AUTOSET;
nameOrganization 'Организация' (Chat c) = name(organization(c));
EXTEND FORM messengers
    PROPERTIES (c) nameOrganization
;

// temp
organization = DATA Organization (Chat);
onStarted() + {
    organization[ManagerTo](Chat c) <- organization(c) WHERE organization(c) AND NOT organization[ManagerTo](c);     
}

//EXTEND FORM changeChats
//    OBJECTS o = Organization BEFORE cm
//    FILTERS organization(ch) = o
//;
dialogChangeChats(ControllerManager cmm) + {
    DIALOG changeChats OBJECTS cm = cmm FILTERS organization(ch) = organization(cmm);
}

CONSTRAINT organization(controllerDevice(ControllerManager cm)) != organization(cm) CHECKED BY controllerDevice[ControllerManager] MESSAGE 'Организация расписания не соответсвует организации устройства';
CONSTRAINT organization(ManagerTo ch) != organization(ControllerManager cm) AND in(ch, cm) MESSAGE 'Организация чата не соответсвует организации устройства';

overNew(ControllerManager cm, ControllerDevice cd) + {
    organization(cm) <- organization(cd);
}