MODULE Device;

CLASS ABSTRACT DeviceType;
name 'Имя' = ABSTRACT BPISTRING[1000] (DeviceType);

CLASS Device 'Устройство';
id 'ИД' = DATA LONG (Device);
type 'Тип' = ABSTRACT DeviceType (Device);

CLASS ABSTRACT Measurement 'Измерение';
device = DATA Device (Measurement);
deviceId 'ИД устройства' (Measurement m) = id(device(m));
dateTime 'Время измерения' = DATA DATETIME (Measurement);

CLASS ABSTRACT Server 'Сервер';
deviceType = ABSTRACT DeviceType (Server);
nameDeviceType 'Тип устройства' (Server s) = name(deviceType(s));
isRunning = DATA BOOLEAN (Server);
startServerInternal ABSTRACT (Server);
stopServerInternal ABSTRACT (Server);
startServer 'Запустить сервер' (Server server) {    
    APPLY isRunning(server) <- TRUE;
    
    startServerInternal(server);
}
stopServer 'Остановить сервер' (Server server) {
    stopServerInternal(server);

    APPLY isRunning(server) <- NULL;
}

server = GROUP AGGR Server server BY deviceType(server);
server(Device device) = server(type(device)); 
device = GROUP AGGR Device device BY server(device), id(device);
server(Measurement m) = server(device(m));

FORM servers 'Сервера' 
    OBJECTS s = Server
    PROPERTIES (s) nameDeviceType, startServer SHOWIF NOT isRunning(s), stopServer SHOWIF isRunning(s)
    
    OBJECTS d = Device 
    PROPERTIES (d) id, delete = DELETE
    FILTERS type(d) = deviceType(s) 
    
    OBJECTS m = Measurement
    PROPERTIES (m) deviceId, dateTime
    FILTERS server(m) = s
    ORDER dateTime(m) DESC
;

DESIGN servers {
    OBJECTS {
        NEW tab {
            type = SPLITH ;
            MOVE BOX(d) {
                caption = 'Устройства';
                flex = 1;
            }
            MOVE BOX(m) {
                caption = 'Измерения';
                flex = 3;
            }
            fill = 10;
        }
    }
}

NAVIGATOR {
    NEW servers;
}

//CLASS ABSTRACT Indicator;
//deviceType = ABSTRACT DeviceType (Indicator);
//number = ABSTRACT INTEGER (Indicator);
